// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Doing "~zx & x" would have worked, but notzx here needs to be all 1s or 0s
    // right now the code below is just setting the LSB to 1 or 0 and the rest are 0s.
    // Nand2Tetris doesn’t give an easy “broadcast bit to bus” primitive so i'll
    // just use MUX i.e. can't turn 1 bit zx to 16 bit ~zx easily.

    //Not(in=zx, out=notzx);
    //Not(in=zy, out=notzy);
    //And16(a[0]=notzx, b=x, out=zerox);
    //And16(a[0]=notzy, b=y, out=zeroy);
    
    // conditional zeroing of x and y
    Mux16(a=x , b=false, sel=zx, out=zerox);
    Mux16(a=y , b=false, sel=zy, out=zeroy);

    //conditional negation of x and y
    Not16(in=zerox, out=notx);
    Not16(in=zeroy, out=noty);
    Mux16(a=zerox, b=notx, sel=nx, out=transx);
    Mux16(a=zeroy, b=noty, sel=ny, out=transy);

    //addition or conjuction
    And16(a=transx, b=transy, out=conj);
    Add16(a=transx, b=transy, out=sum);
    Mux16(a=conj, b=sum, sel=f, out=res);

    //final negation
    //mutliple outputs so we can implement zr and ng
    Not16(in=res, out=notres);
    Mux16(a=res, b=notres, sel=no, out=out,
     out[0..7]=outLSB, out[8..14]=outMSB, out[15] = outMSBbit);

    // zr implementaion
    // there is no Or16way, and I can't use Or8way with finalOut directly because 
    // 'Internal pins (in this case: finalOut) cannot be subscripted or indexed'
    // so gonna have to do this the hard way.
    // BUT, the langauge does allow multiple outputs and 
    // Sub-busing (indexing) internal pins pg 390

    Or8Way(in=outLSB, out=Or1);
    Or8Way(in[0..6]=outMSB, in[7]=outMSBbit, out=Or2);
    Or(a=Or1, b=Or2, out=notzero);
    Not(in=notzero, out=zr);

    //ng implementation
    Or(a=outMSBbit, b=false, out=ng);

}